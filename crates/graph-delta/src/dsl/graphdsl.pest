WHITESPACE = _{ " " | "\t" | NEWLINE }
NEWLINE = _{ "\n" | "\r\n" }

file = { SOI ~ statement* ~ EOI }

statement = _{
    node_delete_cmd | node_cmd |
    edge_delete_cmd | edge_cmd |
    subgraph_delete_cmd | subgraph_move_cmd | subgraph_cmd |
    graph_set_cmd |
    node_defaults_cmd | edge_defaults_cmd |
    rank_same_cmd | rank_min_cmd | rank_max_cmd
}

// # --- Node operations ---
node_cmd = { "node" ~ identifier ~ (attr_list)? }
node_delete_cmd = { "node" ~ "delete" ~ identifier }

// # --- Edge operations ---
edge_cmd = { "edge" ~ identifier ~ "->" ~ identifier ~ (attr_list)? }
edge_delete_cmd = { "edge" ~ "delete" ~ identifier ~ "->" ~ identifier }

// # --- Subgraph operations ---
subgraph_cmd = { "subgraph" ~ identifier ~ (attr_list)? }
subgraph_move_cmd   = { "subgraph" ~ "move" ~ identifier ~ "to" ~ identifier }
subgraph_delete_cmd = { "subgraph" ~ "delete" ~ identifier }

// # --- Global graph settings ---
graph_set_cmd     = { "graph" ~ "set" ~ attr_list }
node_defaults_cmd = { "node" ~ "defaults" ~ attr_list }
edge_defaults_cmd = { "edge" ~ "defaults" ~ attr_list }

// # --- Rank commands ---
rank_same_cmd = { "rank" ~ "same" ~ identifier_list }
rank_min_cmd  = { "rank" ~ "min" ~ identifier_list }
rank_max_cmd  = { "rank" ~ "max" ~ identifier_list }


// # --- Attribute & identifiers ---
attr_list        = { attr ~ ( ","? ~ attr)* }  // Implicit whitespace, optional comma
attr             = { identifier ~ "=" ~ value }
identifier_list  = { identifier ~ ("," ~ identifier)* }
identifier       = @{ ASCII_ALPHANUMERIC+ | quote }  // Identifiers can be quoted
value            = @{ quote | (!("," | WHITESPACE | NEWLINE) ~ ANY)+ }  // Quoted string or unquoted word

quote = { "\"" ~ (!"\"" ~ ANY)* ~ "\"" }
